plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.teambind'
version = '0.0.1-SNAPSHOT'
description = '팀바인드 이용관리 서버, 이용시간및 예약 가능시간 조회관련 서비스 서버'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    // cause -> ObjectMapper
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // MariaDB driver
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // ShedLock for distributed scheduler lock
    implementation 'net.javacrumbs.shedlock:shedlock-spring:5.10.0'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-redis-spring:5.10.0'

    // Test용 ShedLock JDBC provider와 Spring JDBC 지원
    testImplementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:5.10.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc'

    implementation 'org.springframework.kafka:spring-kafka'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'com.h2database:h2'
    testImplementation 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
}

tasks.named('test') {
    useJUnitPlatform()

    testLogging {
        // 테스트 이벤트 로깅: passed(성공), failed(실패), skipped(스킵)
        events "passed", "skipped", "failed"

        // 예외 전체 내용 출력
        exceptionFormat "full"

        // 각 테스트 메소드 실행 시 표시할 정보
        showExceptions true
        showCauses true
        showStackTraces true

        // 표준 출력/에러 스트림 표시
        showStandardStreams false

        // 테스트 실행 후 요약 정보
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\n테스트 결과: ${result.resultType}"
                println "총 테스트: ${result.testCount}개"
                println "성공: ${result.successfulTestCount}개"
                println "실패: ${result.failedTestCount}개"
                println "스킵: ${result.skippedTestCount}개"
                println "소요 시간: ${result.endTime - result.startTime}ms"
            }
        }
    }
}
